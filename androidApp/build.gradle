apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion versions.compileSdk
  defaultConfig {
    applicationId "me.saket.press"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode 7
    versionName "1.5"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    archivesBaseName = "press_v${versionName}_(${versionCode})"

    def secrets = readSecrets()
    buildConfigField "String", "GITHUB_SSH_PRIV_KEY", "\"${secrets["github_ssh_private_key"] ?: "nicolas_cage"}\""
  }

  lintOptions {
    lintConfig file("lint.xml")
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
    }
    release {
      minifyEnabled false
    }
  }

  kotlinOptions {
    jvmTarget = "1.8"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'META-INF/*'
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

static def readSecrets() {
  Properties secrets = new Properties()
  def secretsFile = new File('shared/secrets.properties')
  if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
  }
  return secrets
}

dependencies {
  implementation project(':wysiwyg')
  implementation project(':shared')
  implementation project(':resourceinterceptor')

  implementation deps.kotlin.stdlib.jdk8
  implementation deps.androidx.appCompat
  implementation deps.androidx.recyclerView
  implementation deps.androidx.browser
  implementation deps.androidx.ktx
  implementation deps.material.core

  implementation deps.contour
  implementation deps.timber
  implementation deps.rx2.core
  implementation deps.rx2.android
  implementation deps.rx2.binding
  implementation deps.rx2.kotlin
  implementation deps.dagger.runtime
  implementation deps.assistedInject.annotations
  kapt deps.dagger.compiler
  kapt deps.assistedInject.processor
  implementation deps.multiplatformUuid
  implementation deps.inboxRecyclerView
  implementation deps.klock.android
  implementation deps.itemAnimators
  implementation deps.betterLinkMovementMethod

  debugImplementation deps.flipper.debug.core
  debugImplementation deps.flipper.debug.soLoader
  releaseImplementation deps.flipper.release.core

  testImplementation deps.junit
  testImplementation deps.robolectric
  testImplementation deps.androidx.test.core

  androidTestImplementation deps.androidx.test.core
  androidTestImplementation deps.androidx.test.runner
  androidTestImplementation deps.androidx.test.jUnit
  androidTestImplementation deps.androidx.test.jUnitKtx
  androidTestImplementation deps.truth

  // Shouldn't be using reaktive directly from platform code, but this is temporary.
  androidTestImplementation deps.reaktive.core.common
  androidTestImplementation deps.reaktive.interop.rxJava2
}
