apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.squareup.sqldelight"

android {
  compileSdkVersion projectVersions.compileSdk
  defaultConfig {
    minSdkVersion projectVersions.minSdk
  }
}

sqldelight {
  PressDatabase {
    packageName = "me.saket.press"
    schemaOutputDirectory = file("src/commonMain/db/databases")
  }
}

// Not sure, but probably used by CocoaPods.
version = 0.1

kotlin {
  android("android")

  def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
  def iosTarget = (buildForDevice) ? iosArm64('ios') : iosX64('ios')
  iosTarget.binaries {
    framework {
      // Disable bitcode embedding for the simulator build.
      if (!buildForDevice) {
        embedBitcode("disable")
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation project(':wysiwyg')
        implementation kotlin('stdlib-common')
        implementation deps.reaktive.core.common
        implementation deps.koin.core.common
        implementation deps.multiplatformUuid
        implementation deps.klock.common
        implementation deps.sqlDelight.runtime.common
        implementation deps.stately
        implementation deps.multiplatformSettings.core
      }
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
        implementation deps.reaktive.testing.common
        implementation deps.reaktive.testing.utils
        implementation deps.multiplatformSettings.testing
        implementation deps.assertK.common
        implementation deps.assertK.opentest4j  // https://youtrack.jetbrains.com/issue/IDEA-214533
      }
    }
    androidMain {
      dependencies {
        implementation kotlin('stdlib')
        implementation deps.androidx.appCompat
        implementation deps.androidx.preferences
        implementation deps.timber
        implementation deps.rx2.core
        implementation deps.reaktive.interop.android
        implementation deps.sqlDelight.driver.android
      }
    }
    androidTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
        implementation deps.androidx.test.jUnit
        implementation deps.robolectric
      }
    }
    iosMain {
      dependencies {
        implementation deps.sqlDelight.driver.iOS
      }
    }
    iosTest {}

    all {
      languageSettings {
        enableLanguageFeature('InlineClasses')
        useExperimentalAnnotation('kotlin.Experimental')
      }
    }
  }
}

dependencies {}

task copyFramework {
  def buildType = project.findProperty("kotlin.build.type") ?: "DEBUG"
  dependsOn "link${buildType.toLowerCase().capitalize()}FrameworkIos"

  doLast {
    def srcFile = kotlin.targets.ios.binaries.getFramework(buildType).outputFile
    def targetDir = getProperty("configuration.build.dir")
    copy {
      from srcFile.parent
      into targetDir
      include 'shared.framework/**'
      include 'shared.framework.dSYM'
    }
  }
}

task iosTest {
  def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
  dependsOn 'linkDebugTestIos'
  group = JavaBasePlugin.VERIFICATION_GROUP
  description = "Runs tests for target 'ios' on an iOS simulator"

  doLast {
    def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
    exec {
      commandLine 'xcrun', 'simctl', 'spawn',  '--standalone', device, binary.absolutePath
    }
  }
}
//tasks.check.dependsOn iosTest
