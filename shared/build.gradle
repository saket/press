apply plugin: "com.android.library"
apply plugin: "kotlin-multiplatform"
apply plugin: "kotlin-android-extensions"
apply plugin: "com.squareup.sqldelight"
apply plugin: "org.jetbrains.kotlin.native.cocoapods"
apply plugin: "com.codingfeline.buildkonfig"
apply plugin: 'kotlinx-serialization'

android {
  compileSdkVersion versions.compileSdk
  defaultConfig {
    minSdkVersion versions.minSdk
  }
  testOptions.unitTests.includeAndroidResources = true
}

sqldelight {
  PressDatabase {
    packageName = "me.saket.press"
    schemaOutputDirectory = file("src/commonMain/db/databases")
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = "1.8"
  }
}

kotlin {
  targets {
    fromPreset(presets.android, "android")
    fromPreset(presets.macosX64, "macos") {
      compilations.main.kotlinOptions.freeCompilerArgs += "-Xobjc-generics"
      binaries {
        framework("shared") {
          isStatic = false
          linkerOpts.add("-lsqlite3")
        }
      }
    }

    def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
    def iosTarget = (buildForDevice) ? presets.iosArm64 : presets.iosX64

    fromPreset(iosTarget, "ios") {
      binaries {
        compilations.main.kotlinOptions.freeCompilerArgs += "-Xobjc-generics"
        framework("shared") {
          isStatic = false
          linkerOpts.add("-lsqlite3")

          // Disable bitcode embedding for the simulator build.
          if (!buildForDevice) {
            embedBitcode("disable")
          }
        }
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation project(':wysiwyg')
        implementation project(':kgit')
        implementation deps.reaktive.core.common
        implementation deps.reaktive.interop.coroutines
        implementation deps.koin.core.common
        implementation deps.multiplatformUuid
        implementation deps.klock.common
        implementation deps.sqlDelight.runtime.common
        implementation deps.stately.common
        implementation deps.stately.concurrency
        implementation deps.stately.isolate
        implementation deps.stately.isoCollections
        implementation deps.multiplatformSettings.core
        implementation deps.ktor.client.core
        implementation deps.ktor.logger
        implementation deps.ktor.kotlinxSerialization
        implementation deps.kotlinxSerialization
      }
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
        implementation deps.reaktive.testing.common
        implementation deps.reaktive.testing.utils
        implementation deps.multiplatformSettings.testing
        implementation deps.assertK.common
        implementation deps.assertK.opentest4j // https://youtrack.jetbrains.com/issue/IDEA-214533
      }
    }
    androidMain {
      dependencies {
        implementation deps.androidx.appCompat
        implementation deps.androidx.preferences
        implementation deps.timber
        implementation deps.rx2.core
        implementation deps.reaktive.interop.rxJava2
        implementation deps.sqlDelight.driver.android
        implementation deps.ktor.client.okHttp
        implementation deps.okio
      }
    }
    androidTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
        implementation deps.androidx.test.jUnit
        implementation deps.sqlDelight.driver.jvm
      }
    }
    nativeMain {
      dependencies {
        implementation deps.sqlDelight.driver.native
        implementation deps.ktor.client.cio
      }
    }
    macosMain {
      dependsOn(nativeMain)
    }
    macosTest {}
    iosMain {
      dependsOn(nativeMain)
    }
    iosTest {}

    all {
      languageSettings {
        enableLanguageFeature('InlineClasses')
        useExperimentalAnnotation('kotlin.Experimental')
      }
    }
  }
}

kotlin.cocoapods {
  summary = "A cross-platform markdown editor"
  homepage = "https://github.com/saket/press"
}
version = 1.0 // also used by cocoapods plugin.

dependencies {}

buildkonfig {
  Properties secrets = new Properties()
  def secretsFile = new File('shared/secrets.properties')
  if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
  }

  packageName = "me.saket.press.shared"
  defaultConfigs {
    buildConfigField "String", "GITHUB_CLIENT_ID", secrets["github_client_id"] ?: "nicolas_cage"
    buildConfigField "String", "GITHUB_CLIENT_SECRET", secrets["github_client_secret"] ?: "nicolas_cage"
    buildConfigField "String", "GIT_TEST_SSH_PRIV_KEY", secrets["git_test_ssh_private_key"] ?: ""
    buildConfigField "String", "GIT_TEST_REPO_SSH_URL", secrets["git_test_repo_ssh_url"] ?: ""
    buildConfigField "String", "GIT_TEST_REPO_BRANCH", secrets["git_test_repo_branch"] ?: ""
  }
}

task checkiOS {
  def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
  dependsOn 'linkDebugTestIos'
  group = JavaBasePlugin.VERIFICATION_GROUP
  description = "Runs tests for target 'ios' on an iOS simulator"

  doLast {
    def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
    exec {
      commandLine 'xcrun', 'simctl', 'spawn', '--standalone', device, binary.absolutePath
    }
  }
}
tasks.check.dependsOn checkiOS
