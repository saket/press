apply plugin: 'com.android.library'
apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlin-android-extensions'
apply plugin: "com.squareup.sqldelight"
apply plugin: "org.jetbrains.kotlin.native.cocoapods"

android {
  compileSdkVersion versions.compileSdk
  defaultConfig {
    minSdkVersion versions.minSdk
  }
}

sqldelight {
  PressDatabase {
    packageName = "me.saket.press"
    schemaOutputDirectory = file("src/commonMain/db/databases")
  }
}

kotlin {
  targets {
    fromPreset(presets.android, 'android')
    fromPreset(presets.macosX64, 'macos') {
      binaries {
        framework('shared') {
          isStatic = false
          linkerOpts.add("-lsqlite3")
        }
      }
    }

    def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
    def iosTarget = (buildForDevice) ? presets.iosArm64 : presets.iosX64

    fromPreset(iosTarget, "ios") {
      binaries {
        framework('shared') {
          isStatic = false
          linkerOpts.add("-lsqlite3")

          // Disable bitcode embedding for the simulator build.
          if (!buildForDevice) {
            embedBitcode("disable")
          }
        }
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation project(':wysiwyg')
        implementation deps.reaktive.core.common
        implementation deps.koin.core.common
        implementation deps.multiplatformUuid
        implementation deps.klock.common
        implementation deps.sqlDelight.runtime.common
        implementation deps.stately
        implementation deps.statelyConcurrency
        implementation deps.multiplatformSettings.core
      }
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
        implementation deps.reaktive.testing.common
        implementation deps.reaktive.testing.utils
        implementation deps.multiplatformSettings.testing
        implementation deps.assertK.common
        implementation deps.assertK.opentest4j  // https://youtrack.jetbrains.com/issue/IDEA-214533
        implementation deps.statelyCollections
      }
    }
    androidMain {
      dependencies {
        implementation kotlin('stdlib')
        implementation deps.androidx.appCompat
        implementation deps.androidx.preferences
        implementation deps.timber
        implementation deps.rx2.core
        implementation deps.reaktive.interop.android
        implementation deps.sqlDelight.driver.android
      }
    }
    androidTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
        implementation deps.androidx.test.jUnit
        implementation deps.robolectric
      }
    }
    macosMain {
      dependencies {
        implementation deps.sqlDelight.driver.native
      }
    }
    macosTest {}
    iosMain {
      dependencies {
        implementation deps.sqlDelight.driver.native
      }
    }
    iosTest {}

    all {
      languageSettings {
        enableLanguageFeature('InlineClasses')
        useExperimentalAnnotation('kotlin.Experimental')
      }
    }
  }
}

kotlin.cocoapods {
  summary = "A cross-platform markdown editor"
  homepage = "https://github.com/saket/press"
}
version = 1.0 // also used by cocoapods plugin.

dependencies {}

task checkiOS {
  def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
  dependsOn 'linkDebugTestIos'
  group = JavaBasePlugin.VERIFICATION_GROUP
  description = "Runs tests for target 'ios' on an iOS simulator"

  doLast {
    def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
    exec {
      commandLine 'xcrun', 'simctl', 'spawn',  '--standalone', device, binary.absolutePath
    }
  }
}
tasks.check.dependsOn checkiOS
