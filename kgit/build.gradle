plugins {
  id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.70'
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'

repositories {
  mavenCentral()
}

kotlin {
  targets {
    fromPreset(presets.jvm, "jvm")
    fromPreset(presets.macosX64, "macos")

    def buildForDevice = project.findProperty("device")?.toBoolean() ?: false
    def iosTarget = (buildForDevice) ? presets.iosArm64 : presets.iosX64
    fromPreset(iosTarget, "ios")
  }

  sourceSets {
    commonMain {
      dependencies {
        implementation kotlin('stdlib-common')
        implementation deps.statelyConcurrency
        implementation deps.kotlinxSerialization.common
      }
    }
    commonTest {
      dependencies {
        implementation kotlin('test-common')
        implementation kotlin('test-annotations-common')
      }
    }
    jvmMain {
      dependencies {
        implementation kotlin('stdlib-jdk8')
        implementation "org.eclipse.jgit:org.eclipse.jgit:5.7.0.202003110725-r"
        implementation deps.kotlinxSerialization.jvm
      }
    }
    jvmTest {
      dependencies {
        implementation kotlin('test')
        implementation kotlin('test-junit')
      }
    }
    nativeMain {
      dependsOn(commonMain)
      dependencies {
        implementation deps.kotlinxSerialization.native
      }
    }
    nativeTest { dependsOn(commonTest) }

    macosMain { dependsOn(nativeMain) }
    macosTest { dependsOn(nativeTest) }

    iosMain { dependsOn(nativeMain) }
    iosTest { dependsOn(nativeTest) }

    all {
      languageSettings {
        enableLanguageFeature("InlineClasses")
      }
    }
  }
}
